plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

android {

    compileSdk 33

    flavorDimensions "versionCode"

    // 1 or more digits
    def versionMajor = 0
    // exactly 1 digit
    def versionMinor = 0
    // exactly 2 digits
    def versionBuild = 01

    defaultConfig {

        applicationId "com.binishmatheww.scanner"

        minSdk 21

        targetSdk 33

        versionCode versionMajor * 1000 + versionMinor * 100 + versionBuild

        versionName "${versionMajor}.${versionMinor}.${versionBuild}"

        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {

        stable {
            buildConfigField 'boolean', 'SHOULD_PRINT_LOGS', "false"
            versionName "${versionMajor}.${versionMinor}.${versionBuild}"
        }

        alpha {
            buildConfigField 'boolean', 'SHOULD_PRINT_LOGS', "true"
            versionName "${versionMajor}.${versionMinor}.${versionBuild}-alpha"
        }

        beta {
            buildConfigField 'boolean', 'SHOULD_PRINT_LOGS', "true"
            versionName "${versionMajor}.${versionMinor}.${versionBuild}-beta"
        }

    }

    applicationVariants.all { variant ->

        variant.outputs.all { output ->

            def project = "The scanner"
            def version = "v"+versionMajor+"."+versionMinor+"."+versionBuild
            //def formattedDate = new Date().format('YYYY_MM_dd_hh_mm_ss_aa')
            def apkName = "${project}_${version}.apk"

            outputFileName = new File(apkName)

        }

    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    

    kotlinOptions {
        jvmTarget = '1.8'
    }


}

dependencies {


    // Kotlin standard lib
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.7.20"

    // Androidx core ktx
    implementation 'androidx.core:core-ktx:1.9.0'

    // Appcompat
    implementation 'androidx.appcompat:appcompat:1.5.1'

    // Material
    implementation 'com.google.android.material:material:1.7.0'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"

    // Activity ktx
    implementation "androidx.activity:activity-ktx:1.6.1"

    //Fragment ktx
    implementation "androidx.fragment:fragment-ktx:1.5.4"

    // GMS vision
    implementation 'com.google.android.gms:play-services-vision:20.1.3'

    // Image cropper
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.8.0'

    // itext pdf lib
    implementation 'com.itextpdf:itextg:5.5.10'
    implementation 'com.madgag.spongycastle:core:1.58.0.0'

    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'


}